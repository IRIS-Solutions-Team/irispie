
    !transition-variables

    "Productivity" a
    "Productivity, Rate of change" roc_a
    "Gross production" y
    "Private consumption" c
    "Private investment" i
    "Stock of capital" k
    "Hours worked" h
    "Real wage rate" w
    "Real interest rate" r
    "Private consumption to GDP ratio" c_to_y
    "Private investment to GDP ratio" i_to_y


    !log-variables !all-but
    c_to_y, i_to_y


    !transition-shocks

    "Shock to productivity" shock_a
    "Shock to consumer preferences" shock_c


    !transition-equations

    log(roc_a) = rho*log(roc_a[-1]) + (1-rho)*log(alpha) + shock_a \
    !! roc_a = alpha;

    c[+1]/c = beta*r*exp(shock_c);

    w = c;

    k = (1 - delta)*k{-1} + i;

    y = (a*h)^(1-gamma) * k{-1}^gamma;

    gamma*y = k{-1} * (r - 1 + delta);

    (1-gamma)*y = w * h;

    y = i + c;

    c_to_y = c / y;

    i_to_y = i / y;

    roc_a = roc(a);

